clear stack

# define a set of words to experiment with
# this could be expanded so that we just define words as a regexp
# in a way that resemble Finnish words
#
# TODO: define a rule instead of using a lexicon
read lexc test_lexicon.lexc

define Lexicon ;
regex Lexicon ;

define Vowel     a | e | i | o | u | y | ä | ö ;
define LongVowel     a a | e e | i i | o o | u u | y y | ä ä | ö ö ;
define Const 	b | c | d | f | g | h | j | k | l | m | n | p | q | r | s | t | v;

define AnyLetter [Vowel |  Const];

# In non-loan words 'd', 'j', 'v' cannot be at the end of a syllable
define LastConst Const - [d | j v ];

define Diphthong 	a i | e i | o i | ä i | ö i | e y | ä y | ö y | a u | e u | o u | u i | y i | i u | i y | i e | u o | y ö ;


# Syllable structure for native words
define CV 	[ Const Vowel ];
define CVC	[ Const Vowel LastConst ];
define CVCC	[ Const Vowel Const LastConst ];
define CVV	[ Const Vowel Vowel ];
define CVVC	[ Const Vowel Vowel LastConst ];
define V	[ Vowel ];
define VC	[ Vowel LastConst ];
define VCC 	[ Vowel Const LastConst ];
define VV	[ LongVowel ];
define VVC	[ Vowel Vowel LastConst ];

# Syllable structures in loan words
define CCV	[ Const Const Vowel ];
define CCVC	[ Const Const Vowel Const ];
define CCVCC	[ Const Const Vowel Const Const ];
define CCVV	[ Const Const Vowel Vowel ];
define CCVVC 	[ Const Const Vowel Vowel Const ];
define sCCVC	[ 's' [Const Const Vowel Const ]];
define sCCVCC	[ 's' [Const Const Vowel Const Const ]];


define OpenSyllable [CV | CVV | V | VV ];  
define ClosedSyllable [ CVC | CVCC | CVVC | VC | VCC | VVC ];

define End [%- | %^];

define AllSyllables [
	# native word structures: 
	CVVC |CV | CVC | CVCC | CVV | VC | VCC | VVC | VV | V | 
	# loan word structures:
	CCV | CCVC | CCVCC | CCVV | CCVVC | sCCVC | sCCVCC  
];


###########################################################################
#
# The two basic rules from Institute for the Languages of Finland:
#
# http://www.kielitoimistonohjepankki.fi/haku/tavutus/ohje/153
#
# Syllable border is always before a combination of consonant and a vowel
#
# 1) Tavuraja on aina konsonantin ja vokaalin yhdistelmän edellä
# 
# Adding AllSyllables in the context to prevent adding a hyphen to the
# beginning of the word
define syl [. .] -> %- || AllSyllables _ Const [ Vowel | Diphthong ] ; 

# Syllable border is between vowels that do not form a diphthong
# 2) Tavuraja on sellaisten eri vokaalien välissä, jotka eivät muodosta diftongia
define nonDiphthongSplit a -> a %- || _ [e | o | y ] .o. # a-e, a-o, a-y
		       e -> e %- || _ [a | o | y ] .o. # e-a, e-o, e-y
		       i -> i %- || _ [a | o ] .o.     # i-a, i-o
                       o -> o %- || _ [a | e ] .o.     # o-a, o-e
                       u -> u %- || _ [a | e ] .o.    # u-a, u-e
                       0 -> 0;              #

# TODO the above rules do not catch all syllables, especially in loan words
#
#
define cleanup %^ -> 0 ;

regex Lexicon .o.
        syl .o.
        nonDiphthongSplit .o.
	cleanup
;
